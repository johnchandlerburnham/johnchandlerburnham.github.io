---
title: "Notes (HPFP 09/31): Lists"
author: jcb
date: 2017-11-01
tags: hpfp, notes, haskell


---

# 9 Lists

## 9.5 Using ranges to construct lists

### Exercise: EnumFromTo

```haskell
--09/EnumFromTo.hs

module EnumFromTo where

-- the exercises for specific types

eftBool:: Bool -> Bool -> [Bool]
eftBool x y = go x y []
  where
    go a b c
      | a > b = c
      | a == b = reverse (a : c)
      | otherwise = go (succ a) b (a : c)

eftInt:: Int -> Int -> [Int]
eftInt x y = go x y []
  where
    go a b c
      | a > b = c
      | a == b = reverse (a : c)
      | otherwise = go (succ a) b (a : c)

eftOrd:: Ordering -> Ordering -> [Ordering]
eftOrd x y = go x y []
  where
    go a b c
      | a > b = c
      | a == b = reverse (a : c)
      | otherwise = go (succ a) b (a : c)

eftChar:: Char -> Char -> [Char]
eftChar x y = go x y []
  where
    go a b c
      | a > b = c
      | a == b = reverse (a : c)
      | otherwise = go (succ a) b (a : c)

-- these all look basically the same, so let's generalize:

eft :: (Ord a, Enum a) => a -> a -> [a]
eft x y = go x y []
  where
    go a b c
      | a > b = c
      | a == b = reverse (a : c)
      | otherwise = go (succ a) b (a : c)

-- that reverse is clunky though, let's see if we can get rid of it

eft2 :: (Ord a, Enum a) => a -> a -> [a]
eft2 x y = go x y []
  where
    go a b c
      | a > b = c
      | a == b = c ++ a:[]
      | otherwise = go (succ a) b (c ++ a:[])

-- using string concatenation is just as slow though, let's use recursion

eft3 :: (Ord a, Enum a) => a -> a -> [a]
eft3 x y
  | x > y = []
  | x == y = [x]
  | otherwise = x : eft3 (succ x) y

-- We can get rid of the Ord constraint by leveraging the mapping between
-- an Enum and Int

eft4 :: Enum a => a -> a -> [a]
eft4 x y
  | fromEnum x > fromEnum y = []
  | fromEnum x == fromEnum y = [x]
  | otherwise = x : eft4 (succ x) y
```

## 9.6 Extracting portions of lists

### Exercises: Thy Fearful Symmetry

```haskell
--09/FearfulSymmetry.hs
module FearfulSymmetry where

split :: String -> [String]
split [] = []
split  x = word : split rest
  where
    word = takeWhile (/= ' ') x
    rest = (drop 1) $ dropWhile (/= ' ') x

splitOn :: Char -> String -> [String]
splitOn _ [] = []
splitOn  c str = part : (splitOn c rest)
  where
    part = takeWhile (/= c) str
    rest = (drop 1) $ dropWhile (/= c) str

firstSen = "Tyger Tyger, burning bright\n"
secondSen = "In the forests of the night\n"
thirdSen = "What immortal hand or eye\n"
fourthSen = "Could frame thy fearful symmetry?"
sentences = firstSen ++ secondSen ++ thirdSen ++ fourthSen

myLines :: String -> [String]
myLines x = splitOn '\n' x

shouldEqual =
  [ "Tyger Tyger, burning bright"
  , "In the forests of the night"
  , "What immortal hand or eye"
  , "Could frame thy fearful symmetry?"
  ]

main :: IO ()
main = print $ "Are they equal? " ++ show (myLines sentences == shouldEqual)
```

## 9.7 List Comprehensions

### Exercises: Comprehend Thy Lists

1. `[4, 16]`
2. `[]`
3. `[]`, unless `mySqr` is from `[1..10]`

### Exercises: Square Cube

```haskell
--09/SquareCube.hs
module SquareCube where

mySqr = [x^2 | x <- [1..5]]
myCube = [x^3 | x <- [1..5]]

exercise1 = [(x, y) | x <- mySqr, y <- myCube]
exercise2 = [(x, y) | x <- mySqr, y <- myCube, x < 50, y < 50]
exercise3 = length exercise2
```

## 9.8 Spines and nonstrict evaluation

### Exercises: Bottom Madness

1. no
2. yes
3. no
4. yes
5. no
6. yes
7. no
8. yes
9.  yes
10. no

### Intermission: Is it in normal form?

1. WHNF & NF
2. WHNF
3. neither
4. neither
5. neither
6. neither
7. WHNF

## 9.9 Transforming lists

### Exercises: More Bottoms

1. bottom
2. yes
3. bottom
4. Is this character a vowel?
  `itIsMystery :: Char -> Bool`
5.  a. the first 10 square numbers
    b. `[1, 10, 20]`
    c. `[15, 15, 15]`

6.  ```haskell
    --09/foldbool.hs
    foldBool = map (\x -> (Data.Bool.bool x (-x) (x == 3)))
    ```

## 9.10 Filtering lists of values

### Exercises: Filtering

```haskell
-- 09/Filtering.hs
module Filtering where

filterThreeMult :: [Integer] -> [Integer]
filterThreeMult = filter (\x -> x `mod` 3 /= 0)

howManyThreeMults :: [Integer] -> Int
howManyThreeMults x = length x - (length . filterThreeMult) x

howManyThreeMults' :: [Integer] -> Int
howManyThreeMults' = length . filter (\x -> x `mod` 3 == 0)

removeArticle :: String -> [String]
removeArticle = filter (not . isArticle) . words
  where isArticle x = elem x ["a", "an", "the"]
```

### Zipping exercises

```haskell
-- 09/ZippingExercises.hs
module ZippingExercises where

myZip :: [a] -> [b] -> [(a, b)]
myZip (x:xs) (y:ys) = (x, y) : myZip xs ys
myZip _ _ = []

myZipWith :: (a -> b -> c) -> [a] -> [b] -> [c]
myZipWith f (x:xs) (y:ys) = (f x y) : (myZipWith f xs ys)
myZipWith _ _ _ = []
```

## 9.12 Chapter Exercises

### Data.Char

```haskell
--09/CharExercises.hs
module CharExercises where

import Data.Char

--1
-- isUpper :: Char -> Bool
-- toUpper :: Char -> Char

-- 2
filterUpper = filter isUpper

-- 3
capFirst :: String -> String
capFirst (x:xs) = (toUpper x):xs
capFirst _ = ""

-- 4
strToUpper :: String -> String
strToUpper (x:xs) = (toUpper x):(strToUpper xs)
strToUpper _ = ""

-- 5
headToUpper :: String -> Char
headToUpper = toUpper . head

-- 6, wrote it pointfree the first time...
```

### Ciphers

```haskell
--09/Cipher.hs
module Cipher where

import Data.Char

caesar :: Int -> String -> String
caesar key string = go key $ (map toLower . filter isAlpha) string
  where go _ "" = ""
        go n (c:cs) = chr ((ord c + n - ord 'a') `mod` 26 + ord 'a') : go n cs

unCaesar :: Int -> String -> String
unCaesar key string = caesar (negate key) string

test :: Int -> String -> Bool
test n s = (map toLower . filter isAlpha) s == (unCaesar n . caesar n) s
```

### Writing your own standard functions

```haskell
--09/StdFunc.hs
module StdFunc where

-- 1
myOr :: [Bool] -> Bool
myOr [] = False
myOr (x:xs) = if x == True then True else myOr xs

-- 2
myAny :: (a -> Bool) -> [a] -> Bool
myAny f xs = (myOr . map f) xs

-- 3
myElem :: Eq a => a -> [a] -> Bool
myElem x xs = myAny ((==) x) xs

-- 4
myReverse :: [a] -> [a]
myReverse xs = go xs []
  where go [] n = n
        go (n:ns) a = go ns (n:a)

-- 5
squish :: [[a]] -> [a]
squish [] = []
squish ((n:[]):nss) = n : squish (nss)
squish ((n:ns):nss) = n : squish (ns:nss)

-- 6
squishMap :: (a -> [b]) -> [a] -> [b]
squishMap f xs = go f xs []
  where go _ [] [] = []
        go f (x:xs) [] = go f xs (f x)
        go f xs (a:as) = a : (go f xs as)

-- 7
squishAgain :: [[a]] -> [a]
squishAgain = squishMap id

-- 8
myMaximumBy :: (a -> a -> Ordering) -> [a] -> a
myMaximumBy cmp (x:xs) = go cmp xs x
  where go _ [] a = a
        go cmp (x:xs) a = go cmp xs (if (cmp x a) == GT then x else a)

-- 9
myMinimumBy :: (a -> a -> Ordering) -> [a] -> a
myMinimumBy cmp (x:xs) = go cmp xs x
  where go _ [] a = a
        go cmp (x:xs) a = go cmp xs (if (cmp x a) == LT then x else a)


myMaximum :: (Ord a) => [a] -> a
myMaximum = myMaximumBy compare

myMinimum :: (Ord a) => [a] -> a
myMinimum = myMinimumBy compare
```

## 9.14 Follow-up resources

1. [Data.List documentation for the base library.](http://hackage.haskell.org/package/base/docs/Data-List.html)

2. [Ninety-nine Haskell problems.](https://wiki.haskell.org/H-99:_Ninety-Nine_Haskell_Problems)

---
