---
title: "Notes (HPFP 17/31): Applicative"
author: jcb
date: 2017-11-01
tags: notes, haskell, hpfp
---

# 17 Applicative

## 17.5 Applicative in use

### Exercises: Lookups
```haskell
--17/Lookups.hs
module Lookups where

import Data.List (elemIndex)

-- 1
added :: Maybe Integer
added = (+3) <$> (lookup 3 $ zip [1, 2, 3] [4, 5, 6])

-- 2
y :: Maybe Integer
y = lookup 2 $ zip [1, 2, 3] [4, 5, 6]

z :: Maybe Integer
z = lookup 2 $ zip [1, 2, 3] [4, 5, 6]

tupled :: Maybe (Integer, Integer)
tupled = (,) <$> y <*> z

tupled2 :: Maybe (Integer, Integer)
tupled2 = (pure (,)) <*> y <*> z

-- that's cool, the first tupled builds the function ((,) y) inside the maybe
-- the second tupled lifts (,) inside the maybe then applies both args to it

-- 3
x3 :: Maybe Int
x3 = elemIndex 3 [1, 2, 3, 4, 5]

y3 :: Maybe Int
y3 = elemIndex 4 [1, 2, 3, 4, 5]

max' :: Int -> Int -> Int
max' = max

maxed :: Maybe Int
maxed = (pure max') <*> x3 <*> y3

-- 4
xs = [1, 2, 3]
ys = [4, 5, 6]

x4 :: Maybe Integer
x4 = lookup 3 $ zip xs ys

y4 :: Maybe Integer
y4 = lookup 2 $ zip xs ys

summed :: Maybe Integer
summed = (pure sum) <*> ((,) <$> x4 <*> y4)

summed2 :: Maybe Integer
summed2 = sum <$> ((,) <$> x4 <*> y4)
```

## Exercises: Identity Instance
```haskell
--17/Identity.hs
module Identity where

newtype Identity a = Identity a deriving (Eq, Ord, Show)

instance Functor Identity where
  fmap f (Identity a) = Identity (f a)

instance Applicative Identity where
  pure f = (Identity f)
  (<*>) (Identity f) (Identity a) = Identity (f a)
```
## Exercise: Fixer Upper
```haskell
--17/FixerUpper.hs
module FixerUpper where

-- 1
one = const <$> Just "Hello" <*> (pure "World")

-- 2
two = (,,,) <$> Just 90 <*> Just 10 <*> Just "Tierness" <*> pure [1, 2, 3]
```

## List Applicative Exercise
```haskell
--17/applicativeexercises/src/ListApplicative.hs
module ListApplicative where

import Control.Applicative
import Data.Monoid
import Test.QuickCheck
import Test.QuickCheck.Checkers
import Test.QuickCheck.Classes

data List a = Nil | Cons a (List a) deriving (Eq, Show)

instance Functor List where
  fmap f (Cons a as) = Cons (f a) (fmap f as)
  fmap _ Nil = Nil

instance Applicative List where
  pure x = Cons x Nil
  (<*>) _  Nil = Nil
  (<*>) Nil _ = Nil
  (<*>) fs as = flatMap (flip fmap as) fs
--  (<*>) (Cons f fs) as = (fmap f as) `append` ((<*>) fs as)
--  (<*>) fs as = flatMap (\f -> fmap f as) fs

instance Arbitrary a => Arbitrary (List a) where
  arbitrary = do
    as <- arbitrary
    return (mkList as)

instance Eq a => EqProp (List a) where
  (=-=) = eq

newtype ZipList' a = ZipList' (List a) deriving (Eq, Show)

instance Eq a => EqProp (ZipList' a) where
  xs =-= ys = xs' `eq` ys'
    where xs' = let (ZipList' l) = xs
                in take' 3000 l
          ys' = let (ZipList' l) = ys
                in take' 3000 l

instance Functor ZipList' where
  fmap f (ZipList' xs) = ZipList' $ fmap f xs

instance Applicative ZipList' where
  pure a = ZipList' $ repeat' a
  (<*>) (ZipList' fs) (ZipList' xs) = ZipList' (zipWith' fs xs)

instance Arbitrary a => Arbitrary (ZipList' a) where
  arbitrary = do
    as <- arbitrary
    return (ZipList' as)

zipWith' :: List (a -> b) -> List a -> List b
zipWith' Nil _ = Nil
zipWith' _ Nil = Nil
zipWith' (Cons f fs) (Cons x xs) = Cons (f x) $ zipWith' fs xs

take' :: Int -> List a -> List a
take' n xs = go n Nil xs where
  go 0 ys _ = ys
  go n ys Nil = ys
  go n ys (Cons x xs) = go (n - 1) (Cons x ys) xs

repeat' :: a -> List a
repeat' a = Cons a $ (repeat' a)

append :: List a -> List a -> List a
append Nil ys = ys
append (Cons x xs) ys = Cons x (append xs ys)

fold :: (a -> b -> b) -> b -> List a -> b
fold _ b Nil = b
fold f b (Cons h t) = f h (fold f b t)

concat' :: List (List a) -> List a
concat' = fold append Nil

flatMap :: (a -> List b) -> List a -> List b
flatMap f as = concat' (fmap f as)

mkList :: [a] -> List a
mkList xs = foldr Cons Nil xs

main :: IO ()
main = do
  let test = [(1, 2, 3), (4, 5, 6)] :: [(Int, Int, Int)]
  quickBatch $ applicative (mkList test)
  quickBatch $ applicative (ZipList' $ mkList test)
```

## ZipList Applicative Exercise
see `applicativeexercises/src/ListApplicative.hs`

## Exercise: Variations on Either
```haskell
--17/applicativeexercises/src/VariationEither.hs
module VariationEither where

import Control.Applicative
import Data.Monoid
import Test.QuickCheck
import Test.QuickCheck.Checkers
import Test.QuickCheck.Classes

data Validation' e a = Failure' e | Success' a deriving (Eq, Show)

instance Functor (Validation' e) where
  fmap f (Success' a) = Success' (f a)
  fmap f (Failure' e) = Failure' e

instance Monoid e => Applicative (Validation' e) where
  pure a = Success' a
  (<*>) (Failure' e) (Failure' r) = Failure' (e `mappend` r)
  (<*>) _           (Failure' e) = Failure' e
  (<*>) (Failure' e) _           = Failure' e
  (<*>) (Success' a) (Success' b) = Success' (a b)

instance (Arbitrary e, Arbitrary a) => Arbitrary (Validation' e a) where
  arbitrary = do
    a <- arbitrary
    e <- arbitrary
    oneof [return (Failure' e), return (Success' a)]

instance (Eq e, Eq a) => EqProp (Validation' e a) where
  (=-=) = eq

main :: IO ()
main = do
  let test :: Validation' (String, String, String) (String, String, String)
      test = Success' ("a", "b", "c")
  quickBatch $ applicative test
```


## 17.9 Chapter Exercises
```haskell
--17/applicativeexercises/src/ChapterExercises.hs
module ChapterExercises where

import Control.Applicative
import Data.Monoid
import Test.QuickCheck
import Test.QuickCheck.Checkers
import Test.QuickCheck.Classes

-- Specialize
-- 1
listPure :: a -> [a]
listPure = pure

listApply :: [(a -> b)] -> [a] -> [b]
listApply = (<*>)

-- 2
ioPure :: a -> IO a
ioPure = pure

ioApply :: IO (a -> b) -> IO a -> IO b
ioApply = (<*>)

-- 3
tuplePure  :: (Monoid a, Monoid c) => a -> (c, a)
tuplePure = pure

tupleApply :: (Monoid a, Monoid c) => (c, (a -> b)) -> (c, a) -> (c, b)
tupleApply = (<*>)

-- 4
funcPure :: a -> (e -> a)
funcPure = pure

funcApply :: (e -> (a -> b)) -> (e -> a) -> (e -> b)
funcApply = (<*>)

-- Instances
-- 1
data Pair a = Pair a a deriving (Eq, Show)

instance Arbitrary a => Arbitrary (Pair a) where
  arbitrary = do
    a <- arbitrary
    a' <- arbitrary
    return (Pair a a')

instance Functor Pair where
  fmap f (Pair a a') = Pair (f a) (f a')

instance Applicative Pair where
  pure a = Pair a a
  (<*>) (Pair f f') (Pair a a') = Pair (f a) (f' a')

instance (Eq a) => EqProp (Pair a) where
  (=-=) = eq

-- 2
data Two a b = Two a b deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b) => Arbitrary (Two a b) where
  arbitrary = do
    a' <- arbitrary
    b' <- arbitrary
    return (Two a' b')

instance Functor (Two a) where
  fmap f (Two a b) = Two a (f b)

instance Monoid a => Applicative (Two a) where
  pure x = Two mempty x
  (<*>) (Two a b) (Two c d) = Two (a <> c) (b d)

instance (Eq a, Eq b) => EqProp (Two a b) where
  (=-=) = eq

-- 3
data Three a b c = Three a b c deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b, Arbitrary c) => Arbitrary (Three a b c) where
  arbitrary = do
    a' <- arbitrary
    b' <- arbitrary
    c' <- arbitrary
    return (Three a' b' c')

instance Functor (Three a b) where
  fmap f (Three a b c) = Three a b (f c)

instance (Monoid a, Monoid b) => Applicative (Three a b) where
  pure x = Three mempty mempty x
  (<*>) (Three a b c) (Three e f g) = Three (a <> e) (b <> f) (c g)

instance (Eq a, Eq b, Eq c) => EqProp (Three a b c) where
  (=-=) = eq

-- 4

data Three' a b = Three' a b b deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b) => Arbitrary (Three' a b) where
  arbitrary = do
    a <- arbitrary
    b <- arbitrary
    b' <- arbitrary
    return (Three' a b b')

instance Functor (Three' a) where
  fmap f (Three' a b b') = Three' a (f b) (f b')

instance (Monoid a) => Applicative (Three' a) where
  pure x = Three' mempty x x
  (<*>) (Three' a b c) (Three' e f g) = Three' (a <> e) (b f) (c g)

instance (Eq a, Eq b) => EqProp (Three' a b ) where
  (=-=) = eq

-- 5

data Four a b c d = Four a b c d deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b, Arbitrary c, Arbitrary d) =>
  Arbitrary (Four a b c d) where
    arbitrary = do
      a' <- arbitrary
      b' <- arbitrary
      c' <- arbitrary
      d' <- arbitrary
      return (Four a' b' c' d')

instance Functor (Four a b c) where
  fmap f (Four a b c d) = Four a b c (f d)

instance (Monoid a, Monoid b, Monoid c) => Applicative (Four a b c) where
  pure x = Four mempty mempty mempty x
  (<*>) (Four a b c d) (Four e f g h) = Four (a <> e) (b <> f) (c <> g) (d h)

instance (Eq a, Eq b, Eq c, Eq d) => EqProp (Four a b c d) where
  (=-=) = eq

-- 6

data Four' a b = Four' a a a b deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b) => Arbitrary (Four' a b) where
    arbitrary = do
      a <- arbitrary
      a' <- arbitrary
      a'' <- arbitrary
      b <- arbitrary
      return (Four' a a' a'' b)

instance Functor (Four' a) where
  fmap f (Four' a a' a'' b) = Four' a a' a'' (f b)

instance (Monoid a) => Applicative (Four' a) where
  pure x = Four' mempty mempty mempty x
  (<*>) (Four' a b c d) (Four' e f g h) = Four' (a <> e) (b <> f) (c <> g) (d h)

instance (Eq a, Eq b) => EqProp (Four' a b) where
  (=-=) = eq

-- Combinations

stops :: String
stops = "pbtdkg"

vowels :: String
vowels = "aeiou"

combos :: [a] -> [b] -> [c] -> [(a, b, c)]
-- combos a b c = liftA3 (\a b c -> (a, b, c)) a b c
combos a b c = liftA3 (,,) a b c

-- Testing

test = (1, 2, 3)

main :: IO ()
main = do
  let test = ("a", "b", "c")
  quickBatch $ applicative (Pair test test)
  quickBatch $ applicative (Two test test)
  quickBatch $ applicative (Three test test test)
  quickBatch $ applicative (Three' test test test)
  quickBatch $ applicative (Four test test test test)
  quickBatch $ applicative (Four' test test test test)
```

## 17.11 Follow-up resources

1. [Tony Morris; Nick Partridge; Validation library](http://hackage.haskell.org/package/validation)

2. [Conor McBride; Ross Paterson; Applicative Programming with Effects](http://staff.city.ac.uk/~ross/papers/Applicative.html)

3. Jeremy Gibbons; Bruno C. d. S. Oliveira; Essence of the Iterator
Pattern

4. [Ross Paterson; Constructing Applicative Functors](http://staff.city.ac.uk/~ross/papers/Constructors.html)

5. Sam Lindley; Philip Wadler; Jeremy Yallop; Idioms are oblivi-
ous, arrows are meticulous, monads are promiscuous.
Note:Idiom means applicative functor and is a useful search
term for published work on applicative functors.

---
