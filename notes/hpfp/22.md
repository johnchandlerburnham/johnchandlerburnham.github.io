---
title: "Notes (HPFP 22/31): Reader"
author: jcb
date: 2017-11-01
tags: notes, haskell, hpfp
---

## 22 Reader

## 22.2 A new beginning

### Short Exercise: Warming Up

```haskell
--22/WarmingUp.hs
module WarmingUp where

import Data.Char

cap :: [Char] -> [Char]
cap xs = map toUpper xs

rev :: [Char] -> [Char]
rev xs = reverse xs

composed :: [Char] -> [Char]
composed = cap . rev

fmapped :: [Char] -> [Char]
fmapped = fmap cap rev

tupled :: [Char] -> ([Char], [Char])
tupled = (,) <$> cap <*> rev

tupled' :: [Char] -> ([Char], [Char])
tupled' = do
  c <- cap
  r <- rev
  return (c, r)

tupledBind :: [Char] -> ([Char], [Char])
tupledBind str = cap <$> rev >>= (,) $ str

tupledBind' :: [Char] -> ([Char], [Char])
tupledBind' = rev >>= (\x -> cap >>= \y -> return (x, y))

{-
instance Monad (-> r) where
  return = const
  (>>=) x f = \r -> f (x r) r

    (rev >>= (\x -> cap >>= \y -> return (x, y))) str
 -> (\x -> cap >>= \y -> return (x, y)) (rev str) str
 -> (cap >>= \y -> return (rev str, y)) str
 -> (\y -> return (rev str, y)) (cap str) str
 -> (return (rev str, cap str)) str
 -> (rev str, cap str)
-}
```

## 22.5 But uh, Reader?

## Exercise: Ask
```haskell
--22/Ask.hs
module Ask where

import Control.Monad
import Control.Applicative

newtype Reader r a = Reader { runReader :: r -> a }

ask :: Reader a a
ask = Reader id
```

## 22.6 Functions have an Applicative too

## Exercise: ReadingComprehension

```haskell
--22/ReadingComprehension.hs
{-# LANGUAGE InstanceSigs #-}
module ReadingComprehension where

import Control.Monad
import Control.Applicative

newtype Reader r a = Reader { runReader :: r -> a }

-- 1
myLiftA2 :: Applicative f => (a -> b -> c) -> f a -> f b -> f c
myLiftA2 f x y = f <$> x <*> y

-- 2
asks :: (r -> a) -> Reader r a
asks f = Reader f

--3
instance Functor (Reader r) where
  fmap f (Reader r) = Reader $ f . r

instance Applicative (Reader r) where
  pure :: a -> Reader r a
  pure a = Reader $ const a
  (<*>) :: Reader r (a -> b) -> Reader r a -> Reader r b
  (<*>) (Reader rab) (Reader ra) = Reader $ \r -> rab r (ra r)

-- Reader Monad
-- 1
instance Monad (Reader r) where
  return = pure
  (>>=) :: Reader r a -> (a -> Reader r b) -> Reader r b
  (>>=) (Reader ra) aRb = Reader $ \r -> runReader (aRb (ra r)) r
```

## 22.7 The Monad of functions
### Exercise: Reader Monad

see `22/ReadingComprehension.hs`

```haskell
--22/Person.hs
module Person where

import Control.Monad
import Control.Applicative

newtype Reader r a = Reader { runReader :: r -> a }

newtype HumanName = HumanName String deriving (Eq, Show)

newtype DogName = DogName String deriving (Eq, Show)

newtype Address = Address String deriving (Eq, Show)

data Person = Person { humanName :: HumanName
                     , dogName :: DogName
                     , address :: Address
                     } deriving (Eq, Show)

data Dog = Dog { dogsName :: DogName
               , dogsAddress :: Address
               } deriving (Eq, Show)

pers :: Person
pers = Person (HumanName "Big Bird")
              (DogName "Barkley")
              (Address "Sesame Street")

chris :: Person
chris = Person (HumanName "Chris Allen")
               (DogName "Papu")
               (Address "Austin")

getDog :: Person -> Dog
getDog p = Dog (dogName p) (address p)

getDogR :: Person -> Dog
getDogR = Dog <$> dogName <*> address

getDogRM' :: Reader Person Dog
getDogRM' =  Reader $ Dog <$> dogName <*> address
```

## 22.11 Chapter Exercises

```haskell
--22/ReaderPractice.hs
module ReaderPractice where

import Control.Applicative
import Data.Maybe

x = [1, 2, 3]
y = [4, 5, 6]
z = [7, 8, 9]

xs :: Maybe Integer
xs = lookup 3 $ zip x y

ys :: Maybe Integer
ys = lookup 6 $ zip y z

zs :: Maybe Integer
zs = lookup 4 $ zip x y

z' :: Integer -> Maybe Integer
z' n = lookup n $ zip x z

x1 :: Maybe (Integer, Integer)
x1 = (,) <$> xs <*> ys

x2 :: Maybe (Integer, Integer)
x2 = (,) <$> ys <*> zs

x3 :: Integer -> (Maybe Integer, Maybe Integer)
x3 n = (,) zn zn where zn = z' n

summed :: Num c => (c, c) -> c
summed (x, y) = x + y

bolt :: Integer -> Bool
bolt = (&&) <$> (>3) <*> (<8)

sequA :: Integral a => a -> [Bool]
sequA m = sequenceA [(>3), (<8), even] m

s' :: Maybe Integer
s' = summed <$> ((,) <$> xs <*> ys)

main :: IO ()
main = do
  print $
    sequenceA [Just 3, Just 2, Just 1]
  print $ sequenceA [x, y]
  print $ sequenceA [xs, ys]
  print $ summed <$> ((,) <$> xs <*> ys)
  print $ fmap summed ((,) <$> xs <*> zs)
  print $ bolt 7
  print $ fmap bolt z
  print $ sequenceA [(>3), (<8), even] 7
-- 1
  print $ foldr (&&) True $ sequA 7
-- 2
  print $ fromMaybe [] $ sequA <$> s'
-- 3
  print $ fromMaybe False $ bolt <$> ys
```

Rewriting Shawty: I'm gonna come back to this. I'm not the biggest
fan of the url shortener exercise. It's in this weird midpoint between
too complicated to totally understand without prior background
and not complicated enough to actually be fun to play with.

## 22.13 Follow-up resources

1. Reader Monad; All About Monads
https://wiki.haskell.org/All_About_Monads
2.Reader Monad; Programming with Monads; Real World Haskell

---
