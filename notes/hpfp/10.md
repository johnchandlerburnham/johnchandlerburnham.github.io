---
title: "Notes (HPFP 10/31): Folding Lists"
author: jcb
date: 2017-11-01
tags: hpfp, notes, haskell
---

# 10 Folding lists

Okay, so here's the thing about the term "catamorphism":

"Kata" in Greek means "down". The opposite of "kata" is "ana" which means "up".

So we have "catamorphisms" and "anamorphisms". Remember that "morph" means
"form", so a "catamorphism" is a "down-form thing" and an "anamorphism"
is an "up-form thing".

But what the heck do "up" and "down" have to do with "forms". There's a
metaphor that recurs (so to speak) again and again in functional programming between height and complexity: Things that have more structure are
upwards and things that have less structure are downwards. It's like a tall building: the more structure you have the higher you go.

So an `Integer` is pretty simple, and is downwards of `[Integer]` or `Maybe
Integer` or `Map String Integer`.

Functions that go "upwards" in this complexity-space, like from `Integer ->
[Integer]` are, roughly speaking, anamorphisms. Functions that go "downwards"
are catamorphisms.


## 10.4 Fold right

### Exercises: Understanding folds

```haskell
module Folds where

-- 1:
one = (==) (foldr (*) 1 [1..5]) (foldl (*) 1 [1..5])

-- 2
two = scanl (flip (*)) 1 [1..3]

--3: c

--4: reduce structure

--5
fiveA = foldr (++) "" ["woot", "WOOT", "woot"]
fiveB = foldr max 'a' "fear is the little death"
fiveC = foldr (&&) True [False, True]
fiveD = foldr (||) True [False, True]
fiveE = foldr ((++) . show) "" [1..5]
fiveF = foldl const 'a'  [1..5]
fiveG = foldl const 0 "tacos"
fiveH = foldr (flip const) 0 "burritos"
fiveI = foldr (flip const) 'z' [1..5]
```

### Exercises: Database Processing

```haskell
module DatabaseProcessing where

import Data.Time

data DatabaseItem = DbString String
                 | DbNumber Integer
                 | DbDate   UTCTime
                 deriving (Eq, Ord, Show)

theDatabase :: [DatabaseItem]
theDatabase =
 [ DbDate (UTCTime
           (fromGregorian 1911 5 1)
           (secondsToDiffTime 34123))
 , DbNumber 9001
 , DbString "Hello, World!"
 , DbDate (UTCTime
           (fromGregorian 1921 5 1)
           (secondsToDiffTime 34123))
 ]

-- 1
filterDbDate :: [DatabaseItem] -> [UTCTime]
filterDbDate = map unpack . filter isADbDate where
 isADbDate (DbDate _) = True
 isADbDate _ = False
 unpack (DbDate utc) = utc

filterDbDate2 :: [DatabaseItem] -> [UTCTime]
filterDbDate2 = foldr unpackDate [] where
 unpackDate (DbDate utc) acc = utc : acc
 unpackDate _ acc = acc

-- 2
filterDbNumber :: [DatabaseItem] -> [Integer]
filterDbNumber = foldr unpackNum [] where
 unpackNum (DbNumber num) acc = num : acc
 unpackNum _ acc = acc

-- 3
mostRecent :: [DatabaseItem] -> UTCTime
mostRecent db = foldr compareDate base db where
 compareDate (DbDate utc) acc = max utc acc
 compareDate _ acc = acc
 base = (filterDbDate2 db) !! 0

-- 4
sumDb :: [DatabaseItem] -> Integer
sumDb = foldr addNum 0 where
 addNum (DbNumber int) acc = int + acc
 addNum _ acc = acc

-- 5
avgDb :: [DatabaseItem] -> Double
avgDb db = (fromIntegral $ fst tup) / (fromIntegral $ snd tup) where
 tup = foldr addNum (0,0) db
 addNum (DbNumber int) (num,den) = (int + num, den + 1)
 addNum _ acc = acc

avgDb2 :: [DatabaseItem] -> Double
avgDb2 db = n / d where
 n = (fromIntegral $ sumDb db)
 d = (fromIntegral $ length $ filterDbNumber db)
```

## 10.9 Scans

### Scans Exercises

```haskell
module Scans where

fibs = 1 : scanl (+) 1 fibs
fibsN x = fibs !! x

--1
fibsToN n = take n $ fibs

--2
fibsLessThan n = takeWhile (< n) fibs

--3
factorials = 1 : scanl ratio 1 factorials
 where ratio m n = (m `div` n + 1) * m

factorials2 = scanl (*) 1 [1..]
lazyCaterers = scanl (+) 1 [1..]
```
## 10.10 Chapter Exercises

### Warm-up and reveiw

```haskell
--10/WarmUp.hs
module WarmUp where

-- 1
stops = "pbtdkg"
vowels = "aeiou"

-- 1a
stopVowelStop = [(a, b, c) | a <- stops, b <- vowels, c <- stops]

-- 1b
pVowelStop = [('p', b, c) | b <- vowels, c <- stops]

-- 1c
nouns = ["cat", "dog", "ball", "box"]
verbs = ["throws", "catches", "jumps", "fetches"]

nounVerbNoun =   [(a, b, c) | a <- nouns, b <- verbs, c <- nouns]

-- 2
seekritFunc x = div (sum (map length (words x))) (length (words x))

-- function is average word length
avgWordLength :: String -> Int
avgWordLength x = div totalWordLengths numberOfWords
 where
   wordList = words x
   numberOfWords = length wordList
   wordLengths = map length wordList
   totalWordLengths = sum wordLengths

-- 3
preciseAvgWordLength :: String -> Double
preciseAvgWordLength x = totalWordLength / numberOfWords
 where
   totalWordLength = fromIntegral $ sum $ map length $ words x
   numberOfWords   = fromIntegral $ length $ words x

```

### Rewriting functions using folds

```haskell
--10/FunctionsUsingFolds.hs
module FunctionsUsingFolds where

myAnd :: [Bool] -> Bool
myAnd = foldr (&&) True

-- 1
myOr :: [Bool] -> Bool
myOr = foldr (||) False

-- 2
myAny :: (a -> Bool) -> [a] -> Bool
myAny f x = foldl check False x where
 check x y = x || f y

-- 3

myElem :: Eq a => a -> [a] -> Bool
myElem x xs = myAny ((==) x) xs

-- 4
myReverse :: [a] -> [a]
myReverse = foldl (flip (:)) []

-- 5
myMap :: (a -> b) -> [a] -> [b]
myMap f xs = foldr ((:) . f) [] xs

-- 6
myFilter :: (a -> Bool) -> [a] -> [a]
myFilter f xs =  foldr g [] xs where
 g x y = if (f x) then (x : y) else y

-- 7
squish :: [[a]] -> [a]
squish = foldr (++) []

-- 8
squishMap :: (a -> [b]) -> [a] -> [b]
squishMap f = foldr ((++) . f) []

-- 9
squishAgain :: [[a]] -> [a]
squishAgain = squishMap id

-- 10
myMaximumBy :: (a -> a -> Ordering) -> [a] -> a
myMaximumBy ord xs = foldr1 g xs
 where g x y = if (ord x y) == GT then x else y

-- 11
myMinimumBy :: (a -> a -> Ordering) -> [a] -> a
myMinimumBy ord xs = foldr1 g xs
 where g x y = if (ord x y) == LT then x else y
```

## 10.12 Follow-up resources

1. [Haskell Wiki. Fold.](https://wiki.haskell.org/Fold)

2. [Richard Bird. Sections 4.5 and 4.6 of Introduction to Functional](Programming using Haskell (1998).)

3. Antoni Diller. Introduction to Haskell.

4. [Graham Hutton. A tutorial on the universality and expressive-
ness of fold.](http://www.cs.nott.ac.uk/~gmh/fold.pdf)

---
