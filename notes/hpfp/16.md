---
title: "Notes (HPFP 16/31): Functor"
author: jcb
date: 2017-11-01
tags: notes, haskell, hpfp
---
# 16  Functor

## 16.4 Let's talk about 4, baby

### Excercises: Be Kind

1. `*`
2. `b` is `* -> *`, `T` is `* -> *`
3. `* -> * -> *`

## 16.7 Commonly used functors

### Exercises: Heavy Lifting

```haskell
--16/HeavyLifting.hs
module HeavyLifting where

-- 1
a = (+1) <$> read "[1]" :: [Int]

-- 2
b = (fmap . fmap) (++ "lol") (Just ["Hi,", "Hello"])

-- 3
c = (*2) . (\x -> x - 2)

-- 4
d = ((return '1' ++) . show) . (\x -> [x, 1..3])

-- 5, this one is a lot less scary if you break it into its pieces first
--    the input and output are both IO Integer and the rest is just legos
e :: IO Integer
e = let ioi = readIO "1" :: IO Integer
        changed = read <$> ("123"++) <$> show <$> ioi
    in (*3) <$> changed
```

## 16.10 Exercises: Instances of Func
```haskell
--16/FunctorInstances.hs
module FunctorInstances where

import Test.QuickCheck
import Test.QuickCheck.Function

-- 1
newtype Identity a = Identity a deriving (Eq, Show)

instance Arbitrary a => Arbitrary (Identity a) where
  arbitrary = do
    a' <- arbitrary
    return (Identity a')

instance Functor Identity where
  fmap f (Identity a) = Identity (f a)

-- 2
data Pair a = Pair a a deriving (Eq, Show)

instance Arbitrary a => Arbitrary (Pair a) where
  arbitrary = do
    a <- arbitrary
    a' <- arbitrary
    return (Pair a a')

instance Functor Pair where
  fmap f (Pair a a') = Pair (f a) (f a')

-- 3
data Two a b = Two a b deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b) => Arbitrary (Two a b) where
  arbitrary = do
    a' <- arbitrary
    b' <- arbitrary
    return (Two a' b')

instance Functor (Two a) where
  fmap f (Two a b) = Two a (f b)

-- 4
data Three a b c = Three a b c deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b, Arbitrary c) => Arbitrary (Three a b c) where
  arbitrary = do
    a' <- arbitrary
    b' <- arbitrary
    c' <- arbitrary
    return (Three a' b' c')

instance Functor (Three a b) where
  fmap f (Three a b c) = Three a b (f c)

-- 5
data Three' a b = Three' a b b deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b) => Arbitrary (Three' a b) where
  arbitrary = do
    a <- arbitrary
    b <- arbitrary
    b' <- arbitrary
    return (Three' a b b')

instance Functor (Three' a) where
  fmap f (Three' a b b') = Three' a (f b) (f b')

-- 6
data Four a b c d = Four a b c d deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b, Arbitrary c, Arbitrary d) =>
  Arbitrary (Four a b c d) where
    arbitrary = do
      a' <- arbitrary
      b' <- arbitrary
      c' <- arbitrary
      d' <- arbitrary
      return (Four a' b' c' d')

instance Functor (Four a b c) where
  fmap f (Four a b c d) = Four a b c (f d)

-- 7
data Four' a b = Four' a a a b deriving (Eq, Show)

instance (Arbitrary a, Arbitrary b) => Arbitrary (Four' a b) where
    arbitrary = do
      a <- arbitrary
      a' <- arbitrary
      a'' <- arbitrary
      b <- arbitrary
      return (Four' a a' a'' b)

instance Functor (Four' a) where
  fmap f (Four' a a' a'' b) = Four' a a' a'' (f b)

-- Testing
type IntToInt = Fun Int Int

functorIdentity :: (Functor f, Eq (f a)) => f a -> Bool
functorIdentity f = fmap id f == f

functorCompose :: (Eq (f c), Functor f) => (a -> b) -> (b -> c) -> f a -> Bool
functorCompose f g x = (fmap g (fmap f x)) == (fmap (g . f) x)

functorCompose' :: (Eq (f c), Functor f) => Fun a b -> Fun b c -> f a -> Bool
functorCompose' (Fun _ f) (Fun _ g) x = (fmap g (fmap f x)) == (fmap (g . f) x)

main :: IO ()
main = do
  quickCheck (functorIdentity :: (Identity Int) -> Bool)
  quickCheck (functorCompose' :: IntToInt -> IntToInt -> (Identity Int) -> Bool)
  quickCheck (functorIdentity :: (Pair Int) -> Bool)
  quickCheck (functorCompose' :: IntToInt -> IntToInt -> (Pair Int) -> Bool)
  quickCheck (functorIdentity :: (Two Int Int) -> Bool)
  quickCheck (functorCompose' :: IntToInt -> IntToInt -> (Two Int Int) -> Bool)
  quickCheck (functorIdentity :: (Three Int Int Int) -> Bool)
  quickCheck (functorCompose' :: IntToInt -> IntToInt -> (Three Int Int Int) -> Bool)
  quickCheck (functorIdentity :: (Three' Int Int) -> Bool)
  quickCheck (functorCompose' :: IntToInt -> IntToInt -> (Three' Int Int) -> Bool)
  quickCheck (functorIdentity :: (Four Int Int Int Int) -> Bool)
  quickCheck (functorCompose' :: IntToInt -> IntToInt -> (Four Int Int Int Int) -> Bool)
  quickCheck (functorIdentity :: (Four' Int Int) -> Bool)
  quickCheck (functorCompose' :: IntToInt -> IntToInt -> (Four' Int Int) -> Bool)
```

8. Trivial doesn't have anything inside it that fmap can apply a function to,
   fmap doesn't make sense for things with kind `*`, or rather fmap on type
   constants is just function application, which is all f and no map.

## 16.11 Ignoring possibilities

### Exercise: Possibly
```haskell
--16/PossiblyEither.hs
module PossiblyEither where

data Possibly a = LolNope | Yeppers a deriving (Eq, Show)

instance Functor Possibly where
  fmap f (Yeppers a) = Yeppers (f a)

data Sum a b = First a | Second b deriving (Eq, Show)


instance Functor (Sum a) where
  fmap f (Second b) = Second (f b)
  fmap _ (First a) = First a
```

### Short Exercise
see PossiblyEither.hs

2.  The `a` in `First a` might be a different type than the `b` in
   `Second b`. The function we pass to fmap can only operate on one of those
types, but not both. In other words the function that fmap maps is of type `b
-> c` and kind `*`. Furthermore, we have to apply the function to `Second b`
rather than `First a`, because the structure that fmaps maps onto is of kind `*
-> *`. Our structure is `(Sum a)` because `Sum` is of kind `* -> * -> *` and
it needs to have accepted every type constructer but the last before its
something that fmap can work on. But that doesn't mean we're barred from
writing another function that does something different to `Sum`, but something
different wont be fmap.


## 16.7 Chapter Exercises

Determine if a valid Fucntor can be written for the datatype provided:

1. No, Bool has kind `*` but fmap only works on `* -> *`
2. Yes, note that `False'` and `True'` both take the same type `a`
3. Yes, fmap can ignore `Falsish`
4. Yes... but why...
5. Nope, theres nothing to fmap over, kind `*`

Rearrange the arguments:
```haskell
--16/Rearrange.hs
module Rearrange where

-- 1.
data Sum b a = First a | Second b

instance Functor (Sum e) where
  fmap f (First a)= First (f a)
  fmap f (Second b) = Second b

-- 2.
data Company a c b = DeepBlue a c | Something b

instance Functor (Company e e') where
  fmap f (Something b) = Something (f b)
  fmap _ (DeepBlue a c) = DeepBlue a c

-- 3.
data More b a = L a b a | R b a b deriving (Eq, Show)

instance Functor (More x) where
  fmap f (L a b a') = L (f a) b (f a')
  fmap f (R b a b') = R b (f a) b'
```

Write Functor instances:
```haskell
--16/Instances.hs
{-# LANGUAGE FlexibleInstances #-}
module Instances where

-- 1
data Quant a b = Finance | Desk a | Bloor b

instance Functor (Quant a) where
  fmap f (Bloor b) = Bloor (f b)
  fmap _ (Desk a) = (Desk a)
  fmap _ Finance = Finance

-- 2
data K a b = K a

instance Functor (K a) where
  fmap _ (K a) = (K a)

-- 3
newtype Flip f a b = Flip (f b a) deriving (Eq, Show)

newtype K' a b = K' a

instance Functor (Flip K' a) where
  fmap f (Flip (K' a)) = Flip (K' (f a))

-- 4
data EvilGoateeConst a b = GoatyConst b

instance Functor (EvilGoateeConst a) where
  fmap f (GoatyConst b) = GoatyConst (f b)

-- 5
data LiftItOut f a = LiftItOut (f a)

instance Functor f => Functor (LiftItOut f) where
  fmap f (LiftItOut fa ) = LiftItOut (fmap f fa)

-- 6
data Parappa f g a = DaWrappa (f a) (g a)

instance (Functor f, Functor g) => Functor (Parappa f g) where
  fmap f (DaWrappa fa gb) = DaWrappa (fmap f fa) (fmap f gb)

-- 7

data IgnoreOne f g a b = IgnoringSomething (f a) (g b)

instance (Functor f, Functor g) => Functor (IgnoreOne f g a) where
  fmap f (IgnoringSomething fa gb) = IgnoringSomething fa (fmap f gb)

-- 8
data Notorious g o a t = Notorious (g o) (g a) (g t)

instance (Functor g) => Functor (Notorious g o a) where
  fmap f (Notorious go ga gt) = Notorious go ga (fmap f gt)

-- 9
data List a = Nil | Cons a (List a)

instance Functor List where
  fmap f (Cons a l) = Cons (f a) (fmap f l)
  fmap _ Nil = Nil

-- 10

data GLord a = NoG | OneG a | MoreG (GLord a) (GLord a) (GLord a)

instance Functor GLord where
  fmap f (MoreG g g' g'') = MoreG (fmap f g) (fmap f g') (fmap f g'')
  fmap f (OneG a) = OneG (f a)
  fmap _ NoG = NoG

-- 11
data TalkToMe a = Halt | Print String a | Read (String -> a)

instance Functor TalkToMe where
  fmap f (Read g) = Read (f . g)
  fmap f (Print s a) = Print s (f a)
  fmap _ Halt = Halt
```

## 16.19 Follow-up resources

1. [Haskell Wikibook; The Functor class.](https://en.wikibooks.org/wiki/Haskell/The_Functor_class)

2. Mark P. Jones; A system of constructor classes: overloading and implicit higher-order polymorphism.

3. Gabriel Gonzalez; The functor design pattern.

---
